CALL_METHOD Address("${account}") "lock_fee" Decimal("10");

# Prepare - grab token from the `Hello` component
CALL_METHOD Address("${component}") "free_token";

# Test - assertions
ASSERT_WORKTOP_CONTAINS Address("${resource}");
ASSERT_WORKTOP_CONTAINS_BY_AMOUNT Decimal("1.0") Address("${resource}");

# Test - worktop
TAKE_FROM_WORKTOP Address("${resource}") Bucket("bucket1");
RETURN_TO_WORKTOP Bucket("bucket1");
TAKE_FROM_WORKTOP_BY_AMOUNT Decimal("1.0") Address("${resource}") Bucket("bucket2");
RETURN_TO_WORKTOP Bucket("bucket2");

# Test - auth zone
CALL_METHOD Address("${account}") "create_proof_by_amount" Address("${xrd}") Decimal("5.0");
CREATE_PROOF_FROM_AUTH_ZONE Address("${xrd}") Proof("proof1");
CREATE_PROOF_FROM_AUTH_ZONE_BY_AMOUNT Decimal("2.0") Address("${xrd}") Proof("proof2");
CLONE_PROOF Proof("proof2") Proof("proof3");
DROP_PROOF Proof("proof1");
DROP_PROOF Proof("proof2");
DROP_PROOF Proof("proof3");

# Test - bucket proof
CALL_METHOD Address("${account}") "withdraw" Address("${xrd}") Decimal("5.0");
CLEAR_AUTH_ZONE;
TAKE_FROM_WORKTOP Address("${xrd}") Bucket("xrd");
CREATE_PROOF_FROM_BUCKET Bucket("xrd") Proof("proof4");
CLONE_PROOF Proof("proof4") Proof("proof5");
DROP_PROOF Proof("proof4");
DROP_PROOF Proof("proof5");
RETURN_TO_WORKTOP Bucket("xrd");

# Clean up - deposit resources
CALL_METHOD Address("${account}") "deposit_batch" Expression("ENTIRE_WORKTOP");
